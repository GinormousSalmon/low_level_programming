T 56 K
G K [фиксация начального адреса]

[0] A 3 F  [формирование кода инструкции возврата]
[1] T 24 @ [запись инструкции возврата]

[2] A 1 F  [загрузка в аккумулятор адреса 1-го элемента массива]
[3] A 15 @ [прибавление к аккумулятору инструкции, использующей коэффициенты]
[4] T 15 @ [запись измененной инструкции]

[loop]
[5] A 0 F [загрузка в аккумулятор счетчика]
[6] S 25 @ [уменьшение счетчика на 1]
[7] G 24 @ [если аккумулятор < 0, переход в ячейку 24]
[8] T 0 F [сохранение счетчика]

[9] H 4 F [запись x0 в умножающий регистр]
[10] V 2 D  [умножение ячейки 2 на x0]
[11] L 1024 F  [сдвиг влево на 12 бит]
[12] L 4 F  [сдвиг влево на 4 бита]
[13] T 2 D  [запись результата в ячейку 2]

[14] H 25 @ [запись 1 в умножающий регистр]
[15] V 0 F [умножение a_n на число в умножающем регистре]
[16] R 1 F  [сдвиг вправо на 2 бита]
[17] A 2 D [прибавление числа из ячейки 2]
[18] T 2 D  [запись результата в ячейку 2]

[19] A 25 @ [загрузка в аккумулятор 1]
[20] L 0 D [сдвиг влево на 1]
[21] A 15 @ [прибавление к аккумулятору инструкции, использующей коэффициенты]
[22] T 15 @ [запись измененной инструкции]

[23] E 5 @ [переход к началу цикла]
[24] E 0 F [инструкция возврата из подпрограммы]

[25] P 0 D [1]


G K [фиксация начального адреса]
[0] A 16 @ [загрузка в аккумулятор степени многочлена]
[1] T 0 F  [запись в ячейку 0, очистка аккумулятора]
[2] A 17 @ [загрузка в аккумулятор адреса 2-го элемента]
[3] T 1 F  [запись в ячейку 1, очистка аккумулятора]
[4] A 19 @ [загрузка в аккумулятор старшего коэффициента]
[5] T 2 F  [запись в ячейку 2, очистка аккумулятора]
[6] H 15 @ [запись 1 в умножающий регистр]
[7] V 2 F  [умножение числа из ячейки 2 на число в умножающем регистре]
[8] R 1 F  [сдвиг вправо на 2 бита]
[9] T 2 D  [запись старшего коэффициента в длинную ячейку 2-3]
[10] A 18 @ [загрузка в аккумулятор x0]
[11] T 4 F  [запись в ячейку 4, очистка аккумулятора]

[12] A 12 @  [вызов]
[13] G 56 F  [подпрограммы]
[14] Z 0 F

[15] P 0 D [1]
[16] P 2 F [степень многочлена = 4]
[17] P 20 @ [адрес 2-го элемента массива]
[18] P 1 D [x0 = 3]

[19] P 9 D  [a4 = 19]
[20] P 2 F  [a3 = 4]
[21] P 0 D  [a2 = 1]  [P 17179869181 D] [a2 = -5]
[22] P 5 D  [a1 = 11]
[23] P 10 F [a0 = 20]

EZ PF [переход к исполнению]

